/*
       Licensed to the Apache Software Foundation (ASF) under one
       or more contributor license agreements.  See the NOTICE file
       distributed with this work for additional information
       regarding copyright ownership.  The ASF licenses this file
       to you under the Apache License, Version 2.0 (the
       "License"); you may not use this file except in compliance
       with the License.  You may obtain a copy of the License at

         http://www.apache.org/licenses/LICENSE-2.0

       Unless required by applicable law or agreed to in writing,
       software distributed under the License is distributed on an
       "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
       KIND, either express or implied.  See the License for the
       specific language governing permissions and limitations
       under the License.
 */
cdvPluginPostBuildExtras.add({
    def inAssetsDir = file("src/main/assets")
    if (!inAssetsDir.exists()) {
        // Backwards compatibility for cordova-android < 7.0.0:
        inAssetsDir = file("assets")
    }
    def outAssetsDir = inAssetsDir
    def outFile = new File(outAssetsDir, "cdvasset.manifest")

    def newTask = task("cdvCreateAssetManifest") {
        doLast {
            def contents = new HashMap()
            def sizes = new HashMap()
            contents[""] = inAssetsDir.list()
            def tree = fileTree(dir: inAssetsDir)
            tree.visit { fileDetails ->
                if (fileDetails.isDirectory()) {
                    contents[fileDetails.relativePath.toString()] = fileDetails.file.list()
                } else {
                    sizes[fileDetails.relativePath.toString()] = fileDetails.file.length()
                }
            }

            outAssetsDir.mkdirs()
            outFile.withObjectOutputStream { oos ->
                oos.writeObject(contents)
                oos.writeObject(sizes)
            }
        }
    }
    newTask.inputs.dir inAssetsDir
    newTask.outputs.file outFile
    def preBuildTask = tasks["preBuild"]
    preBuildTask.dependsOn(newTask)
})
